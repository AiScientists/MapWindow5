; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define OcxPath "d:\mw\MapWinGIS"
#define OcxSetupPath OcxPath + '\install'
#define ExeBinPath "..\src\bin\Release"
#define OcxBinPath OcxPath + '\src\bin\win32'
#define CPU "Win32"
#define vcredist "vcredist_x86_2010_sp1.exe"
#define SystemFlag "32bit"
#define MyAppName "MapWindow"
#define MyAppPublisher "MapWindow Open Source GIS Community"
#define MyAppURL "http://www.mapwindow.org"
#define MyAppVersion GetFileVersion(ExeBinPath + '\MapWindow.exe')

#ifdef x64BitVersion
  #define CPU "x64"
  #define vcredist "vcredist_x64_2010_sp1.exe"
  #define OcxBinPath OcxPath + '\src\bin\x64'
  #define SystemFlag "64bit"
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EEA628A7-2E37-4F0C-9157-F8C9BA1A89FB}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\MapWindow5
DisableProgramGroupPage=no
DefaultGroupName={#MyAppName}
LicenseFile=License.rtf
OutputDir=Output
OutputBaseFilename=MapWindow-v{#MyAppVersion}-{#CPU}
SetupIconFile=MapWindow.ico
UninstallDisplayIcon={uninstallexe}
Compression=lzma
SolidCompression=yes
WizardImageFile=WizImage-MW.bmp
WizardSmallImageFile=WizSmallImage-MW.bmp
AppCopyright={#MyAppPublisher}
PrivilegesRequired=poweruser
MinVersion=0,5.01sp3   
ChangesEnvironment=yes
AlwaysShowDirOnReadyPage=True
AlwaysShowGroupOnReadyPage=true
EnableDirDoesntExistWarning=True
UninstallDisplayName=MapWindow GIS uninstall
CompressionThreads=2
LZMANumBlockThreads=2
AppComments=This package will install {#MyAppName} {#MyAppVersion}
AppContact=Paul Meems (info@mapwindow.nl)
VersionInfoCompany=MapWindow Open Source GIS Team [www.mapwindow.org]
VersionInfoCopyright=Mozilla Public License (MPL) 1.1
VersionInfoDescription=MapWindow Open Source GIS [www.mapwindow.org]
VersionInfoProductName={#MyAppName}
VersionInfoProductVersion={#MyAppVersion}
AlwaysShowComponentsList=false

[Components]
Name: "MapWindow"; Description: "MapWindow core component"; Types: full custom compact; Flags: fixed
Name: "NetCDF"; Description: "Add NetCDF support"; Types: full custom
Name: "ECW"; Description: "Add ECW & JPEG2000 support"; Types: full custom
Name: "MrSID"; Description: "Add MrSID support"; Types: full custom

[Files]
;; GDAL
Source: "{#OcxBinPath}\cfitsio.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\gdal200.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\geos.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\geos_c.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\hdf5.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\hdf5_hl.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\iconv.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\libcurl.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\libeay32.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\expat.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\libmysql.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\libpq.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\libtiff.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\lti_dsdk_9.1.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\lti_lidar_dsdk_1.1.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
;Source: "{#OcxBinPath}\NCSEcw.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\netcdf.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\openjp2.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\proj.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\spatialite.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\ssleay32.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\szip.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\tbb.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\xdrdll.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\xerces-c_2_8.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow
Source: "{#OcxBinPath}\zlib1.dll"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow

;; The OCX:
Source: "{#OcxBinPath}\MapWinGIS.ocx"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion {#SystemFlag}; Components: MapWindow

;; IntelliSense:
;Source: "{#OcxSetupPath}\AxInterop.MapWinGIS.xml"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion; Components: MapWindow
;Source: "{#OcxSetupPath}\Interop.MapWinGIS.xml"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion; Components: MapWindow

;; Licenses
;Source: "{#OcxSetupPath}\Licenses\GDALLicense.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion; Components: MapWindow
;Source: "{#OcxSetupPath}\Licenses\GISInternalsLicense.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion; Components: MapWindow
;Source: "{#OcxSetupPath}\Licenses\MapWinGIS.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion; Components: MapWindow

;; GDAL Plugins:
;Source: "{#OcxBinPath}\gdalplugins\gdal_ECW_JP2ECW.dll"; DestDir: "{app}\MapWinGIS\gdalplugins\"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: ECW
Source: "{#OcxBinPath}\gdalplugins\gdal_MrSID.dll"; DestDir: "{app}\MapWinGIS\gdalplugins\"; Flags: ignoreversion {#SystemFlag}; Components: MrSID
Source: "{#OcxBinPath}\gdalplugins\gdal_netCDF.dll"; DestDir: "{app}\MapWinGIS\gdalplugins\"; Flags: ignoreversion {#SystemFlag}; Components: NetCDF
;Source: "{#OcxSetupPath}\Licenses\ECW5License.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: ECW
;Source: "{#OcxSetupPath}\Licenses\MRSIDLicense.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion; Components: MrSID
;Source: "{#OcxSetupPath}\Licenses\NetCDFLicense.rtf"; DestDir: "{app}\Licenses\"; Flags: ignoreversion; Components: NetCDF

;; GDAL data
Source: "{#OcxBinPath}\gdal-data\*"; DestDir: "{app}\MapWinGIS\gdal-data"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: MapWindow
;Source: "{#OcxSetupPath}\PROJ_NAD\*"; DestDir: "{app}\MapWinGIS\PROJ_NAD"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: MapWindow

;; MapWindow 5:
Source: "{#ExeBinPath}\*.dll"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: MapWindow
Source: "{#ExeBinPath}\*.exe"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: MapWindow
Source: "{#ExeBinPath}\*.exe.config"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: MapWindow
Source: "{#ExeBinPath}\*.manifest"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: MapWindow
Source: "{#ExeBinPath}\Projections\*"; DestDir: "{app}\Projections"; Flags: ignoreversion recursesubdirs createallsubdirs {#SystemFlag}; Components: MapWindow

;; To register the ocx:
;Source: "regMapWinGIS.cmd"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion; Components: MapWindow
;Source: "unregMapWinGIS.cmd"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion; Components: MapWindow
;Source: "SetEnv.exe"; DestDir: "{app}\MapWinGIS\"; Flags: ignoreversion; Components: MapWindow

;; VC++ files
;Source: "{#vcredist}"; DestDir: "{tmp}"; Flags: deleteafterinstall ignoreversion {#SystemFlag};

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Messages]
BeveledLabel={#MyAppName}

[Run]
; Install VC++ 2010 if needed:
#ifdef x64BitVersion
;Filename: "{tmp}\{#vcredist}"; Parameters: "/qb"; Flags: waituntilterminated; Check: VCRedistNeedsInstall_x64()
#else
;Filename: "{tmp}\{#vcredist}"; Parameters: "/qb"; Flags: waituntilterminated; Check: VCRedistNeedsInstall_x86()
#endif
;Run some command files:
;Filename: "{app}\MapWinGIS\regMapWinGIS.cmd"; WorkingDir: "{app}"; Flags: runhidden
Filename: "{app}\MapWindow.exe"; Flags: shellexec runasoriginaluser postinstall nowait skipifsilent; Description: "Start MapWindow GIS"

[UninstallRun]
;Filename: "{app}\MapWinGIS\unregMapWinGIS.cmd"; WorkingDir: "{app}"; Flags: runhidden

[Registry]
;; Add location of MapWinGIS to path, needed for netcdf.dll
;;Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: "expandsz"; ValueName: "Path"; ValueData: "{olddata};{app}"; Check: NeedsAddPath(ExpandConstant('{app}'))

[Icons]
;; In start menu:
Name: "{group}\{#MyAppName}"; Filename: "{app}\MapWindow.exe"; WorkingDir: "{app}"; Comment: "Start MapWindow GIS"; Components: MapWindow
;; On desktop:
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\MapWindow.exe"; WorkingDir: "{app}"; Comment: "Start MapWindow GIS"; Components: MapWindow

[Code]
#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF
type
  INSTALLSTATE = Longint;
const
  INSTALLSTATE_INVALIDARG = -2;  // An invalid parameter was passed to the function.
  INSTALLSTATE_UNKNOWN = -1;     // The product is neither advertised or installed.
  INSTALLSTATE_ADVERTISED = 1;   // The product is advertised but not installed.
  INSTALLSTATE_ABSENT = 2;       // The product is installed for a different user.
  INSTALLSTATE_DEFAULT = 5;      // The product is installed for the current user.

  VC_2008_REDIST_X86 = '{FF66E9F6-83E7-3A3E-AF14-8DE9A809A6A4}';
  VC_2008_REDIST_X64 = '{350AA351-21FA-3270-8B7A-835434E766AD}';
  VC_2008_REDIST_IA64 = '{2B547B43-DB50-3139-9EBE-37D419E0F5FA}';
  VC_2008_SP1_REDIST_X86 = '{9A25302D-30C0-39D9-BD6F-21E6EC160475}';
  VC_2008_SP1_REDIST_X64 = '{8220EEFE-38CD-377E-8595-13398D740ACE}';
  VC_2008_SP1_REDIST_IA64 = '{5827ECE1-AEB0-328E-B813-6FC68622C1F9}';
  VC_2008_SP1_ATL_SEC_UPD_REDIST_X86 = '{1F1C2DFC-2D24-3E06-BCB8-725134ADF989}';
  VC_2008_SP1_ATL_SEC_UPD_REDIST_X64 = '{4B6C7001-C7D6-3710-913E-5BC23FCE91E6}';
  VC_2008_SP1_ATL_SEC_UPD_REDIST_IA64 = '{977AD349-C2A8-39DD-9273-285C08987C7B}';
  VC_2008_SP1_MFC_SEC_UPD_REDIST_X86 = '{9BE518E6-ECC6-35A9-88E4-87755C07200F}';
  VC_2008_SP1_MFC_SEC_UPD_REDIST_X64 = '{5FCE6D76-F5DC-37AB-B2B8-22AB8CEDB1D4}';
  VC_2008_SP1_MFC_SEC_UPD_REDIST_IA64 = '{515643D1-4E9E-342F-A75A-D1F16448DC04}';

  VC_2010_REDIST_X86 = '{196BB40D-1578-3D01-B289-BEFC77A11A1E}';
  VC_2010_REDIST_X64 = '{DA5E371C-6333-3D8A-93A4-6FD5B20BCC6E}';
  VC_2010_REDIST_IA64 = '{C1A35166-4301-38E9-BA67-02823AD72A1B}';
  VC_2010_SP1_REDIST_X86 = '{F0C3E5D1-1ADE-321E-8167-68EF0DE699A5}';
  VC_2010_SP1_REDIST_X64 = '{1D8E6291-B0D5-35EC-8441-6616F567A0F7}';
  VC_2010_SP1_REDIST_IA64 = '{88C73C1C-2DE5-3B01-AFB8-B46EF4AB41CD}';

function MsiQueryProductState(szProduct: string): INSTALLSTATE; 
  external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: string): Boolean;
begin
  Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

function VCRedistNeedsInstall_x86(): Boolean;
begin
  // here the Result must be True when you need to install your VCRedist
  // or False when you don't need to, so now it's upon you how you build
  // this statement, the following won't install your VC redist only when
  // the Visual C++ 2008 Redist (x86) and Visual C++ 2008 SP1 Redist(x86)
  // are installed for the current user
  Result := not (VCVersionInstalled(VC_2010_SP1_REDIST_X86));
end;

function VCRedistNeedsInstall_x64(): Boolean;
begin
  // here the Result must be True when you need to install your VCRedist
  // or False when you don't need to, so now it's upon you how you build
  // this statement, the following won't install your VC redist only when
  // the Visual C++ 2008 Redist (x86) and Visual C++ 2008 SP1 Redist(x86)
  // are installed for the current user
  Result := not (VCVersionInstalled(VC_2010_SP1_REDIST_X64));
end;

function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  // look for the path with leading and trailing semicolon
  // Pos() returns 0 if not found
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
;Name: "afrikaans"; MessagesFile: "compiler:Languages\Afrikaans.isl"
;Name: "albanian"; MessagesFile: "compiler:Languages\Albanian.isl"
;Name: "arabic"; MessagesFile: "compiler:Languages\Arabic.isl"
;Name: "basque"; MessagesFile: "compiler:Languages\Basque.isl"
;Name: "belarusian"; MessagesFile: "compiler:Languages\Belarusian.isl"
;Name: "bosnian"; MessagesFile: "compiler:Languages\Bosnian.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
;Name: "bulgarian"; MessagesFile: "compiler:Languages\Bulgarian.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
;Name: "chinesesimp"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"
;Name: "chinesetrad"; MessagesFile: "compiler:Languages\ChineseTraditional.isl"
Name: "corsican"; MessagesFile: "compiler:Languages\Corsican.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
;Name: "estonian"; MessagesFile: "compiler:Languages\Estonian.isl"
Name: "finnish"; MessagesFile: "compiler:Languages\Finnish.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
;Name: "galician"; MessagesFile: "compiler:Languages\Galician.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "greek"; MessagesFile: "compiler:Languages\Greek.isl"
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"
;Name: "icelandic"; MessagesFile: "compiler:Languages\Icelandic.isl"
;Name: "indonesian"; MessagesFile: "compiler:Languages\Indonesian.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
;Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"
;Name: "lithuanian"; MessagesFile: "compiler:Languages\Lithuanian.isl"
;Name: "luxemburgish"; MessagesFile: "compiler:Languages\Luxemburgish.isl"
;Name: "macedonian"; MessagesFile: "compiler:Languages\Macedonian.isl"
;Name: "malaysian"; MessagesFile: "compiler:Languages\Malaysian.isl"
Name: "nepali"; MessagesFile: "compiler:Languages\Nepali.islu"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
;Name: "romanian"; MessagesFile: "compiler:Languages\Romanian.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "serbiancyrillic"; MessagesFile: "compiler:Languages\SerbianCyrillic.isl"
Name: "serbianlatin"; MessagesFile: "compiler:Languages\SerbianLatin.isl"
;Name: "slovak"; MessagesFile: "compiler:Languages\Slovak.isl"
Name: "slovenian"; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
;Name: "swedish"; MessagesFile: "compiler:Languages\Swedish.isl"
;Name: "tatarish"; MessagesFile: "compiler:Languages\Tatar.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

